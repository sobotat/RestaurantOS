plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.25.0'
    id 'java-library'
}

group 'com.restaurantos'
version '0.3'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.9.0'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.restaurantos'
    mainClass = 'com.restaurantos.Main'
}

javafx {
    version = '17.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {

    implementation('org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0')
    implementation 'org.json:json:20220924'                 // Json
    implementation 'com.google.code.gson:gson:2.10'        // Json
    implementation 'org.apache.logging.log4j:log4j-api:2.19.0'  // Log4J
    implementation 'org.apache.logging.log4j:log4j-core:2.19.0' // Log4J
    implementation 'com.aparapi:aparapi:3.0.0'                  // Log4J
    implementation 'mysql:mysql-connector-java:8.0.31'      // MySQL
    implementation 'com.lambdaworks:scrypt:1.4.0'           // Scrypt

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug','--compress', '2','--no-header-files','--no-man-pages']
    forceMerge('log4j-api')

    launcher {
        name = 'RestaurantOS'
    }
    jpackage {
        installerOptions += [
                '--app-version', version,
                '--copyright', 'Copyrigtht 2022 Tomas Sobota',
                '--vendor', 'Tomas Sobota',
                '--license-file', 'LICENSE'
        ]
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            imageOptions += ['--icon', 'src/main/resources/res/app_logo.ico']
            installerOptions += ['--win-per-user-install',
                                 '--win-dir-chooser',
                                 '--win-menu',
                                 '--win-shortcut',]
        }
    }
}

jlinkZip {
    group = 'distribution'
}